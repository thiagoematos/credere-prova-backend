buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.postgresql:postgresql:42.0.0')
        classpath('org.glassfish.jaxb:jaxb-runtime:2.3.1')
        classpath("nu.studer:gradle-jooq-plugin:3.0.2")
    }

}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'nu.studer.jooq'
apply plugin: 'io.spring.dependency-management'

group = 'credere.prova'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jooq:jooq'
    compile('org.springframework.boot:spring-boot-starter-jooq')

    implementation('org.springframework.boot:spring-boot-starter-web')
    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    jooqRuntime 'org.postgresql:postgresql:42.0.0'
    jooqRuntime "com.sun.activation:javax.activation:1.2.0"
    jooqRuntime "org.glassfish.jaxb:jaxb-runtime:2.2.11"

    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

jooq {
    version = '3.11.2'
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/spacial_probe'
            user = 'postgres'
            password = 'postgres'
            schema = 'public'
        }
        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                validationAnnotations = true
                records = true
                javaTimeTypes = true
            }
            target {
                packageName = 'credere.prova.backend.db'
            }
        }
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/spacial_probe'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
}
